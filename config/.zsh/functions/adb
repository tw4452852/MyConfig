emulate -LR zsh

local args devices d
typeset -A devices_map

args=("$@")

zmodload zsh/zutil
zparseopts -a opts s:

# make sure adb server is start
[[ -z ${args[(r)kill-server]} ]] && command adb start-server

# if we has specify a device with -s or just adb devices, run away
if [[ -n ${opts[(r)-s]} || -n ${args[(r)devices]} || -n ${args[(r)kill-server]} ]]; then
	command adb $args
	return
fi

# if push/pull subcommand, add -p if possible
integer push_i=${args[(I)push]}
integer pull_i=${args[(I)pull]}
if (( push_i != 0 )) && [[ ${args[${push_i}+1]} != "-p" ]]; then
	args[${push_i}]=(push -p)
fi
if (( pull_i != 0 )) && [[ ${args[${pull_i}+1]} != "-p" ]]; then
	args[${pull_i}]=(pull -p)
fi

devices=("${(f)$(command adb devices)}")
devices[1]=()

if (( ${#devices} <= 1 )); then
	command adb $args
	return
fi

devices_map=${(z)devices[@]}

select d in ${(k)devices_map}; do
	command adb -s $d $args
	return
done
