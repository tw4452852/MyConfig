#!/usr/bin/env elvish

use flag
use str
use os

fn get_changes {|commit|
  # Strip the context by removing the line number information
  git show $commit 2>$os:dev-null | grep -E '^[+-][^+-]' | order
}

fn main {|&j=(num +inf) up cur|
  var @patches = (git cherry -v $up $cur | grep +)
  var @candidates = []
  if (> (count $patches) 0) {
    var common_base = (git merge-base $up $cur)
    set candidates = [(git log --format='%h' $common_base..$up | peach &num-workers=$j {|commit|
      var @changes = (get_changes $commit)
      put $changes
    })]
  }

  peach &num-workers=$j {|l|
    var @fields = (str:split &max=3 ' ' $l)
    try {
      var @changes = (get_changes $fields[1])

      if (not (has-value $candidates $changes)) {
        fail ""
      }
    } catch e {
      echo $fields[1]' '$fields[2]
    }
  } $patches
}

flag:call $main~ $args
