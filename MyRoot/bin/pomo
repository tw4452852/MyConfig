#!/usr/bin/env janet

(import spork)

(spork/cjanet/begin-jit
  :module-name "sixel-output"
  :pkg-config ["libsixel"])

(spork/cjanet/include <sixel.h>)

(spork/cjanet/cfunction
  sixel-output
  [(pixbuf 'JanetBuffer) width:int height:int channels:int] -> int
  (def (encoder 'sixel-encoder-t))
  (sixel-encoder-new (& encoder) NULL)
  (sixel-encoder-encode-bytes encoder pixbuf->data width height SIXEL_PIXELFORMAT_RGB888 NULL -1)
  (sixel-encoder-unref encoder))

(spork/cjanet/end-jit)

(defn- main [&]
  (def period (* 60 45)) # seconds
  (var left period)
  (defn reset-alarm
    []
    (set left period))

  # SIGUSR1 to reset
  (os/sigaction :usr1 reset-alarm)

  # Move cursor to upper-left and save position
  (prin "\x1b[H\x1b7")
  (flush)

  (def w 480)
  (def h 128)
  (def ch 3)
  (def img (spork/gfx2d/blank w h ch))
  (def buf (img 0))

  (forever
    (spork/gfx2d/rect img 0 0 w h 0xD7FFFF)
    (spork/gfx2d/draw-simple-text img 10 10 5 5 (string/format "%02d:%02d" (div left 60) (mod left 60)) 0xA1A193)

    (prin "\x1b8") # Put cursor back to original position
    (sixel-output buf w (dec h) ch)
    (flush)

    (-- left)
    (when (zero? left)
      (os/shell `gt sh -c 'echo Take a break | less --quit-on-intr && waylock'`)
      (set left period))
    (ev/sleep 1)))


